<html>
<head>
<style>
body {
	margin: 0px;
	background-color: #FFFFFF;
}
</style>

<script type="text/javascript" src="js/processing.js"></script>
<script type="text/javascript" src="js/jquery.js"></script>

<script type="application/processing" data-processing-target="canvas1">

/* @pjs pauseOnBlur="true"; */

float docMaxWidth = document.body.clientWidth;
float docMaxHeight = document.body.clientHeight;

int clickedThisFrame = 0;


Level[] levels = [
	// new Level("the beginning", new GettingStarted(new Dot())),
	// new Level("movement", new Concealer(new Dot())),
	// new Level("it won't always be easy", new BlinkingConcealer(new Dot())),
	// new Level("follow the light", new ProximityConcealer(new Dot())),
	// new Level("target acquired", new TargetAcquiredConcealer(new Dot())),
	// new Level("i am spartacus", new YouOnlyNeedOneConcealer(new Dot())),
	new Level("on the dancefloor", new DiscoConcealer(new Dot())),

];



TitleText title = new TitleText();

user = new User();

int currentLevel = 0;

void setup() {
 	size(docMaxWidth, docMaxHeight, P2D);

}

void mousePressed() {
  clickedThisFrame = 2;
}

void draw() {
	Level level = levels[currentLevel];
	boolean won = level.playLevel(user);
	if (won) {
		currentLevel++;
		Level nextLevel = levels[currentLevel];
		title.setLevelNumber(currentLevel);
		title.setLevelName(nextLevel.name);
	}

	title.draw();

	// counter of frames for which a click event is acceptable.
	clickedThisFrame--;
}

class PositionedText {
	float x = 15, y = 30;
	int r = 0,g = 0,b = 0, a = 255;
	String content;

	void setColor(int r, int g, int b, int a) {
		this.r = r;
		this.g = g;
		this.b = b;
		this.a = a;
	}

	void setGray(int gr, int a) {
		this.setColor(gr, gr, gr, a);
	}

	void changeAlpha(float a) {
		this.a = a;
	}

	void setText(String t) {
		this.content = t;
	}

	void draw() {
		fill(r,g,b,a);
		text(this.content, x, y);
	}

	void setPosition(float x, float y) {
		this.x = x;
		this.y = y;
	}
}

class TitleText extends PositionedText{
	
	String levelNumber, levelName;

	void setLevelNumber(String l) {
		this.levelNumber = l;
	}

	void setLevelName(String l) {
		this.levelName = l;
	}

	void draw() {
		fill(r,g,b,a);
		text("Level " + (levelNumber + 1), x, y);
		text(levelName, x, y + 15);
	}
}

/**
* A level describes a Concealer (which is used to play the game),
* transitions between levels and help text.
*/
class Level {

	Transition in, out;
	boolean completed = false;
	Concealer concealer;
	String name;

	Level(String name, Concealer _concealer) {
		this.name = name;
		this.concealer = _concealer;
		this.in = concealer.intro();
		this.out = concealer.outro();
	}

	/**
	* @return true as long as the user has not won the level and
	* no transition is happening.
	*/
	boolean playLevel(User user) {

		if (in && in.doTransition()) {
			return false;
		}
		if (!completed) {
			boolean play = concealer.play(user);
			if (!play) {
				return false;
			}
			else {
				completed = true;
			}
		}
		if (out && out.doTransition()) {
			return false;
		}
		return true;
	}

}

class Transition {

	int frame = 0, totalFrames = 0;
	Transition next;

	void inTransition() {
		// no-op
	}

	/**
	* @return false when done.
	*/
	boolean doTransition() {
		if (frame >= totalFrames) {
			return next ? next.doTransition() : false;
		}
		inTransition();
		frame++;
		return true;
	}


}

class Delay extends Transition {
	
	Delay(int frameCount, Transition next) {
		totalFrames = frameCount;
		this.next = next;
	}
}

class DarkenBackground extends Transition {

	DarkenBackground() {
		this.totalFrames = 200;
	}

	void inTransition() {
		float gr = 255 * (totalFrames - frame)/totalFrames;
		title.setGray(255 - gr, 255);
		background(gr);
	}
}

class LightenBackground extends Transition {

	LightenBackground() {
		this.totalFrames = 200;
	}

	void inTransition() {
		float gr = 255 * (totalFrames - frame)/totalFrames;
		title.setGray(gr, 255);
		background(255 - gr);
	}
}

class Timer {

	int totalFrameCount, current;
	Object[] callbacks = null;

	Timer(totalFrameCount) {
		this.totalFrameCount = totalFrameCount;
		current = 0;
		this.callbacks = [];
	}

	void addCallback(fn) {
		this.callbacks[this.callbacks.length] = fn;
	}

	void hit() {
		current++;
		if (current % totalFrameCount == 0) {
			for(int i = 0; i < callbacks.length; i++) {
				callbacks[i]();
			}
		}
	}
}

class Locatable {
	float x = 0, y = 0;

	float distanceFrom(Locatable other) {
		return sqrt(pow(x - other.x, 2) + pow(y - other.y, 2));
	}
}

class User extends Locatable{

	void locateAtMouse() {
		x = mouseX;
		y = mouseY;
	}
}

/**
* Utility class to flicker the dot.
* Todo: generalize into generic trig function.
*/
class FramedAlpha {
	
	int frame = 0;
	float adjustRate = 15;
	Object onMinima = null;
	Object onMaxima = null;

	float current, currentMinus1, currentPlus1;


	void adjustAlpha(Dot dot) {
		frame++;

		this.currentMinus1 = this.current;

		if (this.currentPlus1 != null ) {
			this.current = this.currentPlus1;
		} else {
			this.current = 255*(cos((frame/adjustRate) - PI) + 1)/2;
		}
		this.currentPlus1 = 255*(cos(((frame + 1)/adjustRate) - PI) + 1)/2;

		if (this.current > this.currentMinus1 && this.current > this.currentPlus1 && this.onMaxima != null) {
			onMaxima();
		}

		if (this.current < this.currentMinus1 && this.current < this.currentPlus1 && this.onMinima != null) {
			onMinima();
		}		

		dot.changeAlpha(this.current);
	}

	void addOnMinima(fn) {
		onMinima = fn;
	}

	void addOnMaxima(fn) {
		onMaxima = fn;
	}
}

class Tween extends Transition{
  float start = 0, end = 0, change = 0;
  int frame = 0;
  int totalFrames = 0;
  boolean done = false;
  /**
  * If this Tween is used as a Transition, callbacks
  * are invoked each frame.
  */
  Object transitionCallback;

  Tween(float _start, float _end, int _frames) {
  	this(_start, _end, _frames, null);
  }

  Tween(float _start, float _end, int _frames, Object callback) {
    start = _start;
    end = _end;
    change = _end - _start;
    totalFrames = _frames;
    this.transitionCallback = callback;
  }

  boolean done() {
  	return frame >=totalFrames;
  }

  float valueAtFrame() {
  	throw new Exception("not implemented");
  }

  float valueAtNextFrame() {
    return valueAtFrame(1);
  }

  float valueAtCurrentFrame() {
    return valueAtFrame(0);
  }

  float goToNextFrame() {
    float value = valueAtFrame(1);
  	if (transitionCallback) {
  		transitionCallback(value);
	}
    frame++;
    return value;
  }

  boolean doTransition() {
  	float value = goToNextFrame();
  	return !done();
  }

  void setCallback(Object fn) {
  	this.transitionCallback = fn;
  }
}

/**
* Quadratic ease out function
*/
class QuadEaseOut extends Tween{

	QuadEaseOut(float start, float end, int frames){
		super(start, end, frames);
	}

	QuadEaseOut(float start, float end, int frames, Object callback) {
		super(start, end, frames, callback);
	}

	float valueAtFrame(int frameDiff) {
		float progress = (frame + frameDiff) / totalFrames;
		if (progress >= 1) {
		  return start + change;
		}
		return -change * progress*(progress - 2) + start;
	}

}

class Linear extends Tween{

	Linear(float start, float end, int frames) {
		super(start, end, frames);
	}

	Linear(float start, float end, int frames, Object callback) {
		super(start, end, frames, callback);
	}

	float valueAtFrame(int frameDiff) {
		float progress = (frame + frameDiff) / totalFrames;
		if (progress >= 1) {
			return start + change;
		}
		return start + progress*change;
	}
}

class LinearFadeOutOutro extends Linear {

	Object[] objects;

	LinearFadeOutOutro(Object[] objects) {
		this(objects, null);
	}

	LinearFadeOutOutro(Object[] objects, Object callback) {
		super(255, 0, 40, function(float value) {
			background(255,255);
			for(int i = 0; i < objects.length; i++) {
				objects[i].changeAlpha(value);
				objects[i].draw();
			}
			if (callback) {
				callback(value);
			}
		});
	}


}

/**
* Concealer owns the Dot. It decides how it changes over time. Has one
* template function
*/
class Concealer {

	Dot dot;
	
	Concealer(Dot _dot) {
		dot = _dot;
	}

	/**
	* Play a frame with the user. The concealer returns true
	* if the User has won the frame. 
	*/
	boolean play(User user) {
		background(255,255);
		dot.move();
		dot.draw();
		user.locateAtMouse();
		return isHit(user);
	}

	boolean isHit(User user) {
		return user.distanceFrom(dot) <= dot.radius * 3  && clickedThisFrame > 0; // diameter to make it easier
	}

	Transition intro() {
		return null;
	}

	Transition outro() {
		return null;
	}
}

class GettingStarted extends Concealer {

	Tween dotAppear = null;
	PositionedText clickTheDot = new PositionedText();

	GettingStarted(Dot dot) {
		super(dot);
		dotAppear = new Linear(0, 255, 80);
		dot.changeAlpha(0);
		dot.setPosition(docMaxWidth / 2, 400);
		clickTheDot.setText("Click the dot.");
		clickTheDot.setPosition(docMaxWidth/2  - 30, 100);
	}

	boolean play(User user) {
		background(255, 255);

		float dotAlpha = dotAppear.goToNextFrame();

		clickTheDot.draw();

		dot.changeAlpha(dotAlpha);
		dot.draw();

		user.locateAtMouse();

		return isHit(user);
	}

	Transition outro() {
		return new LinearFadeOutOutro([dot, clickTheDot]);
	}
}

class BlinkingConcealer extends Concealer {

	FramedAlpha framedAlpha;

	BlinkingConcealer(Dot _dot){
		super(_dot);
		framedAlpha = new FramedAlpha();
		framedAlpha.addOnMinima(function () {
			_dot.teleport();
		});
	}

	boolean play(User user) {
		framedAlpha.adjustAlpha(dot);
		return super.play(user);
	}
}

class ProximityConcealer extends Concealer {

	ProximityConcealer(Dot _dot){
		super(_dot);
	}

	boolean play(User user) {
		user.locateAtMouse();
		distance = user.distanceFrom(dot);
		if (distance < 100) {
			dot.changeAlpha(255);
		} else {
			dot.changeAlpha(0);
		}

		a =  255.0/pow(2.0, distance/100);
		background(a, 255);
		title.setGray(255 - a, 255);

		dot.move();
		dot.draw();
		
		return isHit(user);
	}

	Transition intro() {
		return new DarkenBackground();
	}
}

class TargetAcquiredConcealer extends Concealer {

	Timer timer = null;

	TargetAcquiredConcealer(Dot dot) {
		super(dot);
		this.timer = new Timer(1000);
		timer.addCallback(function() {
			dot.teleport();
		})
	}

	boolean play(User user) {
		
		user.locateAtMouse();

		int colorWidth = 5;
		background(0,255);

		PImage yPos = createImage(colorWidth, user.y, RGB);
		PImage yNeg = createImage(colorWidth, docMaxHeight - user.y, RGB);

		PImage xPos = createImage(docMaxWidth - user.x, colorWidth, RGB);
		PImage xNeg = createImage(user.x, colorWidth, RGB);

		randomColors = new Object[colorWidth];
		for (int i = 0; i < colorWidth; i++) {
			randomColors[i] = color(random(255), random(255), random(255), 255);
		}
		
		if (user.x < dot.x) {
			for (int i = 0; i < xPos.pixels.length; i++) {
				xPos.pixels[i] = randomColors[i % colorWidth];
			}

		} else {
			for (int i = 0; i < xNeg.pixels.length; i++) {
				xNeg.pixels[i] = randomColors[i % colorWidth];
			}
		}

		if (user.y > dot.y) {
			for (int i = 0; i < yPos.pixels.length; i++) {
				yPos.pixels[i] = randomColors[i % colorWidth];
			}
		} else {
			for (int i = 0; i < yNeg.pixels.length; i++) {
				yNeg.pixels[i] = randomColors[i % colorWidth];
			}
		}

		image(yPos, user.x - colorWidth / 2, 0);
		image(yNeg, user.x - colorWidth / 2, user.y);
		image(xPos, user.x, user.y - colorWidth / 2);
		image(xNeg, 0, user.y - colorWidth / 2);

		dot.move();
		dot.draw();

		return isHit(user);
	}

	Transition intro() {
		return new DarkenBackground();
	}

	Transition outro() {
		return new LightenBackground();
	}
}

class YouOnlyNeedOneConcealer extends Concealer {

	Dot[] otherDots;
	Tween dotAppear;

	YouOnlyNeedOneConcealer(Dot dot) {
		super(dot);
		dot.changeAlpha(0);
		otherDots = new Dot[100];
		for(i = 0; i < otherDots.length; i++) {
			otherDots[i] = new Dot();
			otherDots[i].changeDirection(1.5, 1.5);
			otherDots[i].changeAlpha(0);
		}
		dotAppear = new Linear(0, 255, 90);
	}

	boolean play(User user) {
		background(255,255);
		user.locateAtMouse();

		float dotAlpha = dotAppear.valueAtCurrentFrame();
		moveOthers(dotAlpha);
		dot.changeAlpha(dotAlpha);
		dotAppear.goToNextFrame();

		userDotDist = user.distanceFrom(dot);
		positionAngle = dot.positionAngle(user);

		if (positionAngle < 0) {
			positionAngle = positionAngle + 2*PI;
		}

		// dot.rotateBy((positionAngle - HALF_PI) / 10000*(userDotDist + 1));

		dot.move();
		dot.draw();
		// dot.debug(user);
		
		return isHit(user);
	}

	void moveOthers(float newAlpha) {
		for (int i = 0; i < otherDots.length; i++) {
			Dot target = otherDots[i];

			float angleTo = target.vectorAngle(dot);

			if (angleTo > PI) {
				angleTo = angleTo - (2*PI);
			}

			float aDotDist = target.distanceFrom(dot);

			target.rotateBy( angleTo / (aDotDist+1));

			target.move();
			target.changeAlpha(newAlpha);
			target.draw();

		}
	}

	Transition outro() {
		// YouOnlyNeedOneConcealer self = this;
		return new Linear(255, 0, 40, function(float newAlpha) {
				background(255,255);
				moveOthers(newAlpha);
				dot.changeAlpha(newAlpha);
				dot.draw();
			}
		);
	}
}

class DiscoConcealer extends Concealer {

	DiscoTile[][] tiles = null;

	DiscoConcealer(Dot dot) {
		super(dot);
		int down = 40, across = 40;
		float dimX = docMaxWidth / (float)down;
		float dimY = docMaxHeight / (float)across;
		tiles = new DiscoTile[down][across];
		
		float seed = 0;

		for(int i = 0; i < down; i++) {
			seed+= 10;
			for(int j = 0; j < across; j++) {
				seed+=0.03;
				
				tiles[i][j] = new DiscoTile(
					i*dimX,
					j*dimY,
					dimX + 1,
					dimY + 1,
					seed,
					255
				);
			}
		}
		dot.changeAlpha(255);
	}

	boolean play(User user) {

		boolean won = super.play(user);
		for(int i = 0; i < tiles.length; i++) {
			for(int j = 0; j < tiles[0].length; j++) {
				tiles[i][j].draw();
			}
		}
		// dot.draw();
		return won;
	}

}

class DiscoTile{

	Locatable location;

	long frame = 0;
	float a,width, height;

	/**
	* The tile color changes along a sinoidal curve, and the rate of change for
	* each color is determined by the ^speed value. 
	*/
	float rspeed, gspeed, bspeed;

	DiscoTile(float x, float y, float width, float height, float seed, float a) {
		location = new Locatable();
		location.x = x;
		location.y = y;
		this.width = width;
		this.height = height;

		this.rspeed = 1 + 0.4 * noise(seed);
		this.gspeed = 1 + 0.4 * noise(seed + 0.01);
		this.bspeed = 1 + 0.4 * noise(seed + 0.02);
		this.a = a;
	}

	void draw() {
		noStroke();

		float r = (255/2)*(-cos((rspeed*frame*PI/255))+1);
		float g = (255/2)*(-cos((gspeed*frame*PI/255))+1);
		float b = (255/2)*(-cos((bspeed*frame*PI/255))+1);

		fill(r,g,b, a);
		rect(location.x, location.y, width, height);
		
		frame++;
	}

	void changeAlpha(float a) {
		this.a = a;
	}
}

class Dot extends Locatable{

	float dx = 1.5, dy = 1.5;

	float a = 255;
	float radius = 5;

	Dot() {
		teleport();
	}

	void teleport() {
		this.x = random(100, docMaxWidth);
		this.y = random(100, docMaxHeight);
	}

	void draw() {
		
		noStroke();
		fill(10,0,0, a);
		ellipse(x, y, radius*2, radius*2);
		
	}

	void setPosition(int x, int y) {
		this.x = x;
		this.y = y;
	}

	void move() {
		x += dx;
		y += dy;
		if (x > docMaxWidth || x < 0) {
			dx = -dx;
		}
		if (y > docMaxHeight || y < 0) {
			dy = -dy
		}
	}

	void changeDirection(float _dx, float _dy) {
		dx = _dx;
		dy = _dy;
	}

	void changeAlpha(float _a) {
		a = floor(_a);
	}

	void rotateBy(float theta) {
		if (isNaN(theta)){
			throw new Exception();
		}
		cs = cos(theta);
		sn = sin(theta);

		tempx = dx * cs - dy * sn;
		tempy = dx * sn + dy * cs;

		dx = tempx;
		dy = tempy;

	}

	/**
	* Two dots are traveling along their own respective
	* dy/dx. This function returns the angle between
	* the vectors represented by dy/dx.
	*/
	float vectorAngle(Dot l) {
		float myMagnitude = sqrt(pow(dx,2) + pow(dy,2));
		float otherMagnitude = sqrt(pow(l.dx,2) + pow(l.dy,2));
		float scalarProd = dx * l.dx + dy * l.dy;

		float angle = acos(scalarProd/(myMagnitude * otherMagnitude));
		return isNaN(angle) ? 0 : angle;
	}

	/**
	* One dot is moving along a dy/dx. This function returns
	* the angle between the vector represented by its dy/dx and
	* a vector which represents the dy/dx necessary to arrive at
	* the position of the given dot instance in the current frame.
	*/
	float positionAngle(Locatable l) {
		// dot vector
		float ldx = l.x - x;
		float ldy = l.y - y;

		// determine theta between my dy/dx and dot vector
		float myMagnitude = sqrt(pow(dx,2) + pow(dy,2));
		float otherMagnitude = sqrt(pow(ldx,2) + pow(ldy,2));
		float scalarProd = dx * ldx + dy * ldy;

		float angle = acos(scalarProd/(myMagnitude * otherMagnitude));
		return isNaN(angle) ? 0 : angle;
	}

	void debug(User user) {
		float distance = distanceFrom(user);
		float angle = positionAngle(user);
		fill(0, 255, 0, 255);
		text((round(angle * 100, 4)/100) + "r", x, y+25);

		noFill();
		stroke(0,255,0,255);
		ellipse(x, y, distance*2, distance*2);

		line(x, y, user.x, user.y);

		if (positionAngle < 0) {
			positionAngle = positionAngle + 2*PI;
		}
	}
}

</script>

<script type="text/javascript">
$(function() {
  $("#canvas1").focus()
});
</script>
</head>
<body>
	<canvas id="canvas1" tabindex="1"></canvas>
</body>
</html>